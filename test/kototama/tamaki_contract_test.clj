(ns kototama.tamaki-contract-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.core.capability-repository :as repository]
            [kototama.tamaki-contract :as tamaki-contract]))

(def heartbeat-envelope
  {:tamaki.capability/version 1
   :tamaki.capability/actor ":organism/heartbeat"
   :tamaki.capability/substrate :kototama-wasm
   :tamaki.capability/role :control-guest
   :tamaki.capability/abi {:namespace "actor:host" :version 0}
   :tamaki.capability/imports
   #{:clock-monotonic :sha256-hex :log-write}
   :tamaki.capability/repositories
   (repository/repository-refs-for-imports
    #{:clock-monotonic :sha256-hex :log-write})
   :tamaki.capability/grants
   #{:clock-monotonic :sha256-hex :log-write}
   :tamaki.capability/limits
   {:allow-write-imports? true
    :allow-secret-imports? false
    :max-http-posts 0 :max-http-fetches 0 :max-llm-infers 0
    :allowed-url-prefixes []}
   :tamaki.capability/effect-policy
   {:clock :autonomous :crypto :autonomous
    :storage-write :autonomous}})

(deftest admits-a-bounded-heartbeat-envelope
  (let [report (tamaki-contract/admit heartbeat-envelope)]
    (is (:ok? report))
    (is (= (:tamaki.capability/grants heartbeat-envelope)
           (get-in report [:host-caps :grants])))))

(deftest independently-rejects-unknown-or-under-granted-imports
  (let [unknown (update heartbeat-envelope
                        :tamaki.capability/imports conj :ambient-shell)
        under-granted (update heartbeat-envelope
                              :tamaki.capability/grants disj :log-write)]
    (is (false? (:ok? (tamaki-contract/admit unknown))))
    (is (false? (:ok? (tamaki-contract/admit under-granted))))))

(deftest independently-rejects-capability-repository-drift
  (let [drifted (update heartbeat-envelope
                        :tamaki.capability/repositories pop)
        problems (set (map :problem
                           (:errors (tamaki-contract/admit drifted))))]
    (is (contains? problems :capability-repository-set-mismatch))))

(deftest independently-rejects-capability-definition-cid-drift
  (let [drifted
        (update-in heartbeat-envelope
                   [:tamaki.capability/repositories 0]
                   assoc :capability/definition-cid
                   (:capability/hash-contract-cid
                    (first (:tamaki.capability/repositories
                            heartbeat-envelope))))
        problems (set (map :problem
                           (:errors (tamaki-contract/admit drifted))))]
    (is (contains? problems :capability-repository-set-mismatch))))

(deftest independently-rejects-unbounded-autonomous-egress
  (let [envelope
        (assoc heartbeat-envelope
               :tamaki.capability/imports #{:http-post}
               :tamaki.capability/repositories
               (repository/repository-refs-for-imports #{:http-post})
               :tamaki.capability/grants #{:http-post}
               :tamaki.capability/limits
               {:max-http-posts 1 :allowed-url-prefixes nil}
               :tamaki.capability/effect-policy
               {:network-write :autonomous})
        errors (set (map :error
                         (:errors (tamaki-contract/admit envelope))))]
    (is (contains? errors :network/allowlist-required))
    (is (contains? errors
                   :effect-policy/network-write-needs-human))))
