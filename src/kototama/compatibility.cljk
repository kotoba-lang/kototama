(ns kototama.compatibility
  (:import (java.nio ByteBuffer)
           (java.nio.charset CharacterCodingException CodingErrorAction StandardCharsets)))

(def section-name "kotoba.compatibility")
(def supported {:version 1 :compiler "kotoba-compiler/1"
                :language "kotoba.language/safe-v1"
                :runtime "kotoba-capability-host-v1"
                :tender-role "kototama/component-tender-v1"
                :tender-contract "kotoba.capability-host/v1"})

(defn- reject [code message]
  (throw (ex-info message {:phase :compatibility :kototama.compatibility/code code})))

(defn- uleb [^bytes bytes offset limit]
  (loop [at offset shift 0 value 0]
    (when (or (>= at limit) (> shift 28)) (reject :invalid "invalid unsigned LEB128"))
    (let [part (bit-and 0xff (aget bytes at))
          value' (bit-or value (bit-shift-left (bit-and part 0x7f) shift))]
      (if (zero? (bit-and part 0x80)) [value' (inc at)]
          (recur (inc at) (+ shift 7) value')))))

(defn- read-text [^bytes bytes offset limit]
  (let [[length start] (uleb bytes offset limit) end (+ start length)]
    (when (or (zero? length) (> length 256) (> end limit))
      (reject :invalid "invalid compatibility text length"))
    (try
      [(str (.decode (doto (.newDecoder StandardCharsets/UTF_8)
                       (.onMalformedInput CodingErrorAction/REPORT)
                       (.onUnmappableCharacter CodingErrorAction/REPORT))
                     (ByteBuffer/wrap bytes start length))) end]
      (catch CharacterCodingException _ (reject :invalid "compatibility text is not UTF-8")))))

(defn- header? [^bytes bytes]
  (and (>= (alength bytes) 8)
       (= [0 97 115 109 1 0 0 0] (mapv #(bit-and 0xff (aget bytes %)) (range 8)))))

(defn inspect [^bytes bytes]
  (when-not (header? bytes) (reject :invalid "invalid WebAssembly header"))
  (let [matches
        (loop [offset 8 matches []]
          (if (= offset (alength bytes)) matches
              (let [id (bit-and 0xff (aget bytes offset))
                    [size start] (uleb bytes (inc offset) (alength bytes)) end (+ start size)]
                (when (> end (alength bytes)) (reject :invalid "truncated WebAssembly section"))
                (if (zero? id)
                  (let [[name payload] (read-text bytes start end)]
                    (recur end (cond-> matches (= name section-name) (conj [payload end]))))
                  (recur end matches)))))]
    (when (> (count matches) 1) (reject :duplicate "compatibility section must be unique"))
    (when-let [[start end] (first matches)]
      (when (>= start end) (reject :invalid "empty compatibility section"))
      (let [fields [:compiler :language :kir :target :runtime :value-abi :tender-role :tender-contract]
            [result offset] (reduce (fn [[result at] field]
                                      (let [[value next] (read-text bytes at end)]
                                        [(assoc result field value) next]))
                                    [{:version (bit-and 0xff (aget bytes start))} (inc start)] fields)]
        (when-not (= offset end) (reject :invalid "trailing compatibility metadata"))
        result))))

(defn validate!
  ([bytes] (validate! bytes false))
  ([^bytes bytes required?]
   (let [descriptor (when (or required? (header? bytes)) (inspect bytes))]
     (when (and required? (nil? descriptor)) (reject :missing "Kotoba compatibility metadata is required"))
     (when descriptor
       (doseq [[field expected] supported]
         (when-not (= expected (get descriptor field))
           (reject :mismatch (str "unsupported Kotoba compatibility " (name field)))))
       (when-not (#{"kotoba.kir/v3" "kotoba.kir/v4"} (:kir descriptor))
         (reject :mismatch "unsupported Kotoba KIR identity"))
       (when-not (= "wasm32-kotoba-v1" (:target descriptor))
         (reject :mismatch "Kototama only admits the generic capability-host target")))
     descriptor)))
