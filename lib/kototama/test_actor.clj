#!/usr/bin/env bb
;; actor common-lib tests (portable, bb/JVM).  Run:  bb --classpath lib lib/actor/test_kototama.clj
(ns kototama.test-actor
  (:require [kototama.gates :as g]
            [kototama.membrane :as m]
            [kototama.heartbeat :as h]
            [kototama.didkey :as d]
            [kotoba.lang.text :as str]
            [clojure.test :refer [deftest is run-tests]]))

(def ok-record {:status :dry-run :sources ["ADR-x" "did:web:y"] :cash 0 :server-held-key false :sim-only true})

(deftest gates-compose
  (is (g/may-draft? ok-record))
  (is (empty? (g/why-refused ok-record)))
  (is (= [:G-sources :G-cash-zero :G-keyless]
         (g/why-refused {:sources ["one"] :cash 5 :server-held-key true})))
  (is (not (g/enough-sources? ["one"])))
  (is (g/enough-sources? ["a" "b"]))
  (is (g/cash-zero? 0))
  (is (g/keyless? false))
  (is (not (g/dry-run? :published))))

(deftest membrane-drafts-and-refuses
  (let [drafted (m/draft (assoc ok-record :subject "coverage:x" :body "summary" :disclaimer "MAP not shutoff"))
        refused (m/draft {:subject "y" :sources ["one"]})]
    (is (m/drafted? drafted))
    (is (= ":dry-run" (get-in drafted [:post ":post/status"])))
    (is (false? (get-in drafted [:post ":post/server-held-key"])))
    (is (str/includes? (get-in drafted [:post ":post/body"]) "MAP not shutoff"))
    (is (m/refused? refused))
    (is (= [:G-sources] (:refusal refused)))))

(deftest build-live-refuses
  (is (thrown? clojure.lang.ExceptionInfo (m/build-live {:any 1}))))

(deftest heartbeat-idempotent
  (is (:append? (h/decide [[:db/add "e" ":a" 1]] [[:db/add "e" ":a" 0]])))
  (is (not (:append? (h/decide [[:db/add "e" ":a" 1]] [[:db/add "e" ":a" 1]]))))
  (is (= :no-change (:reason (h/decide [1] [1]))))
  (let [b (h/beat (fn [_] [[:db/add "e" ":a" 1]]) {} [])]
    (is (:append? b))
    (is (= 1 (:count b)))
    (is (= [[:db/add "e" ":a" 1]] (:datoms b)))))

(deftest no-advice-gate
  (is (g/no-advice? "operating income rose; this is a coverage map"))   ; word-boundary: "operating" ≠ "rating"
  (is (not (g/no-advice? "we recommend buy")))
  (is (= "recommend" (g/advice-hit "we recommend buy, target price up")))
  (is (not (g/no-advice? "目標株価を引き上げ")))                          ; JA substring
  (is (thrown? clojure.lang.ExceptionInfo (g/assert-no-advice "strong buy rating")))
  (is (= "clean text" (g/assert-no-advice "clean text"))))

(deftest didkey-encoding
  (is (str/starts-with? (d/did-key (byte-array 32)) "did:key:z6Mk"))
  (is (= "1" (d/base58btc (byte-array [0]))))
  (is (= "2g" (d/base58btc (byte-array [97]))))
  (is (= "did:key:zABC|bafcid" (d/attest-message "did:key:zABC" "bafcid"))))

(when (= *file* (System/getProperty "babashka.file"))
  (let [{:keys [fail error]} (run-tests 'kototama.test-actor)]
    (System/exit (if (zero? (+ fail error)) 0 1))))
